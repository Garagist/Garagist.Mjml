prototype(Garagist.Mjml:Page) < prototype(Neos.Fusion:Component) {
    language = null
    languageDimension = 'language'

    title = ${q(documentNode).property('titleOverride') || q(documentNode).property('title')}

    debugUrl = Neos.Neos:NodeUri {
        node = ${documentNode}
        absolute = true
        additionalParams = ${{mjml: true}}
    }

    prototype(Neos.Neos:NodeUri) {
        absolute = true
    }
    prototype(Neos.Neos:ConvertUris) {
        absolute = true
        forceConversion = true
        externalLinkTarget = ''
        resourceLinkTarget = ''
    }

    renderer = Garagist.Mjml:Presentation.Page {
        @apply.props = ${props}
        lang = Neos.Fusion:Case {
            @process.convert = ${value ? String.replace(value, '_', '-') : null}
            hasLanguage {
                condition = ${props.language}
                renderer = ${props.language}
            }
            hasDimension {
                condition = ${props.languageDimension && Configuration.setting('Neos.ContentRepository.contentDimensions.' + props.languageDimension) != null}
                renderer = ${documentNode.context.dimensions[props.languageDimension][0]}
            }
            hasDefaultLocale {
                defaultLocale = ${Configuration.setting('Neos.Flow.i18n.defaultLocale')}
                condition = ${this.defaultLocale}
                renderer = ${this.defaultLocale}
            }
        }
        @process.convert = Neos.Neos:ConvertUris {
            absolute = true
            forceConversion = true
            externalLinkTarget = ''
            resourceLinkTarget = ''
        }
    }

    @cache {
        mode = 'uncached'
        context {
            1 = 'site'
            2 = 'node'
            3 = 'documentNode'
        }
    }
}
